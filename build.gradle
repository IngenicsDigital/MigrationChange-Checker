buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'jacoco'
    id "maven-publish"
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version "1.1.0"
}

group 'de.pixel.mcc'

sourceCompatibility = JavaVersion.VERSION_11

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc
}

test {
    useJUnitPlatform()

    testLogging {
        events "skipped", "failed"
        showStandardStreams true
    }

    reports {
        html.enabled = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    def signingKey = System.getenv('SIGN_KEY')
    def signingPassword = System.getenv('SIGN_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

publishing {
    publications {
        mmc(MavenPublication) {
            from components.java

            groupId project.group
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = 'A Pixelflut interface for Java and Kotlin written in Kotlin.'
                url = 'https://github.com/PixelGmbH/MigrationChange-Checker'

                licenses {
                    license {
                        name = 'Apache-2.0 Licence'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                organization {
                    name = 'Pixel GmbH'
                    url = 'https://www.pixel.de/'
                }

                developers {
                    developer {
                        id = 'Poeschl'
                        name = 'Markus PÃ¶schl'
                        url = 'https://poeschl.xyz/'
                    }
                }

                scm {
                    url = "https://github.com/PixelGmbH/MigrationChange-Checker"
                }
            }

            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv('OSSRH_USER')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'commons-codec:commons-codec:1.11'

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
}
